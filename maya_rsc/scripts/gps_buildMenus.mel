// This function suppresses recent menus...
global proc checkGPSFileMenu()
{
	$enable = true;

	// Get the recent file list and store it in an array
	python( "import recentFiles" );
	string $fileLs[] = python( "recentFiles.getLs()" );

	// If recent file list contains no entries, disable menu
	if (size($fileLs)==0) $enable = false;
	if (size($fileLs)==1 && $fileLs[0]=="") $enable = false;
	menuItem -edit -enable $enable GPSFileMenuRecentFileItems;

	// Create "Send To" menu items for one-click
	// interop with available applications.
	//
	buildSendToSubMenus;
}


//File Menu//
/////////////

string $fileMenu = "MayaWindow|mainFileMenu";
buildFileMenu();
string $fileWindow_menu = `menu -exists $fileMenu`;
string $fileWindow_menuItems[] = `menu -ia -q $fileMenu`;

menu -edit -postMenuCommand checkGPSFileMenu $fileMenu;

// Delete file menu entries up to item number 12
for ($i=0; $i<=12; $i++) deleteUI -mi $fileWindow_menuItems[$i];

// Delete project window and set project menu items
deleteUI -menuItem projectWindowFileItem;
deleteUI -menuItem setProjectFileItem;

// Delete recent increments and projects menu items
deleteUI -menuItem FileMenuRecentFileItems;
deleteUI -menuItem FileMenuRecentBackupItems;
deleteUI -menuItem FileMenuRecentProjectItems;

//adding GPS entries
setParent -menu $fileMenu;

	// GPS New Scene
	menuItem -insertAfter ""
			 -label "GPS - New"
			 -annotation ("Create a new scene")
			 -command ("source menus2Py; menus2Py.newScene()")
			 GPSNewScene;

	// GPS Open Scene
	menuItem -insertAfter GPSNewScene
			 -label "GPS - Open..."
			 -annotation ("Open a saved scene")
			 -command ("source menus2Py; menus2Py.openScene()")
			 GPSOpenScene; // tmpMenu;

	// GPS Open Recent Scene (edit the existing menu item)
	menuItem -insertAfter GPSOpenScene
			 -subMenu true
			 -label "GPS - Open Recent"
			 -postMenuCommand "source menus2Py; menus2Py.updateRecentFilesMenu(\"GPSFileMenuRecentFileItems\")"
			 GPSFileMenuRecentFileItems;
	setParent -menu ..;

	menuItem -insertAfter GPSFileMenuRecentFileItems
			 -divider true
			 GPSFileDivider1;

	// GPS Save Scene
	menuItem -insertAfter GPSFileDivider1
			 -label "GPS - Save"
			 -annotation ("Save the current scene")
			 -command ("source menus2Py; menus2Py.saveScene()")
			 GPSSaveScene;

	// GPS Save Scene As
	menuItem -insertAfter GPSSaveScene
			 -label "GPS - Save As..."
			 -annotation ("Save scene with a new name")
			 -command ("source menus2Py; menus2Py.saveSceneAs()")
			 GPSSaveSceneAs;

	// GPS - Incremental Save
	menuItem -insertAfter GPSSaveSceneAs
			 -label "GPS - Incremental Save"
			 -annotation "Save the current scene as new version"
			 -command ("source menus2Py; menus2Py.incrementalSave()")
			 GPSIncrementalSave;

	menuItem -insertAfter GPSIncrementalSave
			 -divider true
			 GPSFileDivider2;

	// GPS Launch Icarus UI
	menuItem -insertAfter GPSFileDivider2
			 -label "GPS - Icarus UI..."
			 -annotation ("Launch Icarus UI")
			 -command ("source menus2Py;menus2Py.launchIcarus()")
			 GPSLaunchIcarusUI;

checkGPSFileMenu();


//Window Menu//
///////////////

string $windowMenu = "MayaWindow|mainWindowMenu";
buildViewMenu($windowMenu);
//string $mainWindow_menu = `menu -exists $windowMenu`;
//$mainWindow_menuItems[] = `menu -ia -q $windowMenu`;

//deleting menu default entries
//playblast
//deleteUI -mi $mainWindow_menuItems[18];
//deleteUI -menuItem playblastItem;

setParent -menu $windowMenu;
	menuItem -edit
			 -label ("GPS - Preview")
			 -command ("source menus2Py; menus2Py.gpsPreview()")
			 -annotation ("Launches GPS Preview")
			 playblastItem;
	menuItem -edit
			 -command ("source menus2Py; menus2Py.gpsPreview()")
			 playblastDialogItem;


//Time Slider menu//
////////////////////

string $timelineMenu = "MayaWindow|toolBar6|MainTimeSliderLayout|formLayout9|TimeSliderMenu";
updateTimeSliderMenu $timelineMenu;
//menu -exists $timelineMenu;
//string $timeline_menuItems[] = `menu -ia -q $timelineMenu`;

//deleting menu default entries
//playblast
//deleteUI -mi $timeline_menuItems[15];
deleteUI -menuItem timeSliderPlayblastItem;

//Adding GPS entries
setParent -menu $timelineMenu;
	menuItem -divider true;
	menuItem -label ("GPS - Preview...")
			 -command ("source menus2Py; menus2Py.gpsPreview()")
			 -annotation ("Launches GPS Preview")
			 GPSPlayblastItem;


//Status Line buttons//
///////////////////////

setParent $gStatusLine;

int $iconHeight = 26;

// GPS - New Scene
iconTextButton -edit -image1 "newScene.png"
               -width $iconHeight -height $iconHeight
               -command ("source menus2Py; menus2Py.newScene()")
               -annotation "New scene"
               newSceneButton;

// GPS - Open Scene
iconTextButton -edit -image1 "openScene.png"
               -width $iconHeight -height $iconHeight
               -command ("source menus2Py; menus2Py.openScene()")
               -annotation "Open scene. Right click for recent files."
               openSceneButton;

// GPS - Create a popup menu for Recent Files on the Open Scene button
deleteUI -menu OpenSceneButtonRecentFileItems;
popupMenu -parent openSceneButton OpenSceneButtonRecentFileItems;
	menu -edit -postMenuCommand "source menus2Py; menus2Py.updateRecentFilesMenu(\"OpenSceneButtonRecentFileItems\")" OpenSceneButtonRecentFileItems;
setParent -menu ..;

// GPS - Save Scene
iconTextButton -edit -image1 "saveScene.png"
               -width $iconHeight -height $iconHeight
               -command ("source menus2Py; menus2Py.saveScene()")
               -annotation "Save scene"
               saveSceneButton;

// GPS - Create a popup menu on the Save button for Save As and Incremental Save
string $menu = `popupMenu -parent saveSceneButton`;
	menuItem -label "Incremental Save" -command "source menus2Py; menus2Py.incrementalSave()";
	menuItem -label "Save As..." -command "source menus2Py; menus2Py.saveSceneAs()";

// The following are new buttons added to the end of the status line toolbar...

// GPS - Rename Tools
iconTextButton -image1 "bulkRename.png"
               -width $iconHeight -height $iconHeight
               -command ("source menus2Py.mel;menus2Py.bulkRename()")
               -annotation "Advanced renaming of objects"
               bulkRenameButton;

// GPS - Common Editors
iconTextButton -image1 "editor.png"
               -width $iconHeight -height $iconHeight
               -annotation "Open commonly used editor windows"
               editorsButton;

// GPS - Add popup menu for common editors
string $menu = `popupMenu -b 1 -p editorsButton`;
	menuItem -label "Outliner" -c "OutlinerWindow";
	menuItem -label "Node Editor" -c "NodeEditorWindow";
	menuItem -divider true;
	menuItem -label "Hypershade" -c "HypershadeWindow";
	menuItem -label "UV Texture Editor" -c "TextureViewWindow";
	menuItem -divider true;
	menuItem -label "Graph Editor" -c "GraphEditor";
	menuItem -label "Dope Sheet" -c "DopeSheetEditor";

// GPS - Browse Project Directories
iconTextButton -image1 "browse.png"
               -width $iconHeight -height $iconHeight
               -annotation "Browse project directories"
               browseDirsButton;

// GPS - Create a menu on the browse dir button for different directories
string $menu = `popupMenu -b 1 -p browseDirsButton`;
	menuItem -label "Browse scenes" -c "source menus2Py;menus2Py.browseScenes()";
	menuItem -label "Browse playblasts" -c "source menus2Py;menus2Py.browsePlayblasts()";
	menuItem -label "Browse renders" -c "source menus2Py;menus2Py.browseRenders()";
	menuItem -label "Browse sourceimages" -c "source menus2Py;menus2Py.browseSourceimages()";
	menuItem -label "Browse shot" -c "source menus2Py;menus2Py.browseShot()";
	menuItem -label "Browse job" -c "source menus2Py;menus2Py.browseJob()";

// GPS - Review
iconTextButton -image1 "review.png"
               -width $iconHeight -height $iconHeight
               -command ("source menus2Py.mel;menus2Py.launchHieroPlayer;")
               -annotation "Launch review app"
               reviewButton;

// GPS - Create a menu on the review button for different review software
string $menu = `popupMenu -b 1 -p reviewButton`;
	menuItem -label "HieroPlayer" -c "source menus2Py;menus2Py.launchHieroPlayer";
	menuItem -label "djv_view" -c "source menus2Py;menus2Py.launchDjv";

// GPS - Open Production Board
iconTextButton -image1 "productionBoard.png"
               -width $iconHeight -height $iconHeight
               -command ("source menus2Py.mel;menus2Py.launchProdBoard;")
               -annotation "Open the Production Board"
               launchProdBoardButton;

// GPS - Launch Icarus UI
iconTextButton -image1 "icarus.png"
               -width $iconHeight -height $iconHeight
               -command ("source menus2Py;menus2Py.launchIcarus()")
               -annotation "Launch Icarus UI"
               launchIcarusButton;

// GPS - Print current Job and Shot info
string $jobInfo = `getenv JOB` + " - " + `getenv SHOT`;
text -label "   ";
text -label $jobInfo -h 27;
setParent -menu ..;

// Swap Object Mode / Component Mode buttons (fix for Maya 2014 only)
//if (getApplicationVersionAsFloat == 2014) {
	formLayout -edit
	           -af selectBtnComponent right 0
	           -ac selectBtnObject right 0 selectBtnComponent
	           -ac selectBtnHierComb right 0 selectBtnObject
	           masksForm;
//}
