// [Icarus] userSetup.mel
//
// Nuno Pereira <nuno.pereira@gps-ldn.com>
// Mike Bonnington <mjbonnington@gmail.com>
// (c) 2013-2019 Gramercy Park Studios
//
// Commands to execute at Maya startup.


// Set environment (should be done first)
python("import os, sys");
python("os.environ['IC_ENV'] = 'MAYA'");

// Initialise Icarus environment
python("sys.path.append(os.environ['IC_BASEDIR'])");
python("from core import env__init__; env__init__.append_sys_paths()");
python("from core.app_session import *");

// Set up path remapping for cross-platform support
string $root = `getenv IC_FILESYSTEM_ROOT`;
string $root_win = `getenv IC_FILESYSTEM_ROOT_WIN`;
string $root_osx = `getenv IC_FILESYSTEM_ROOT_OSX`;
string $root_linux = `getenv IC_FILESYSTEM_ROOT_LINUX`;
if ($root != "None")
{
	dirmap -en true;
	if ($root_win != "None") dirmap -m $root_win $root;
	if ($root_osx != "None") dirmap -m $root_osx $root;
	if ($root_linux != "None") dirmap -m $root_linux $root;
}

// Run UI related stuff
int $batchMode = `about -batch`;
if (!$batchMode)
{
	string $vendor = "";
	if (`getenv IC_VENDOR_INITIALS` != "")
		$vendor = (`getenv IC_VENDOR_INITIALS` + " ");

	// Deploy custom tools
	python("from rsc.maya.scripts import icDeployTools; icDeployTools.deploy()");

	// Build custom menus
	source menuBuild;

	// Set up keyboard shortcuts for custom tools
	// New Scene...
	nameCommand "icNewScene" -ann ($vendor + "New Scene") -c "source menus2Py; menus2Py.newScene()";
	hotkey -cmd -k "n" -n "icNewScene";
	hotkey -ctl -k "n" -n "icNewScene";
	// Open Scene...
	nameCommand "icOpenScene" -ann ($vendor + "Open Scene") -c "source menus2Py; menus2Py.openScene()";
	hotkey -cmd -k "o" -n "icOpenScene";
	hotkey -ctl -k "o" -n "icOpenScene";
	// Save Scene...
	nameCommand "icSaveScene" -ann ($vendor + "Save Scene") -c "source menus2Py; menus2Py.saveScene()";
	hotkey -cmd -k "s" -n "icSaveScene";
	hotkey -ctl -k "s" -n "icSaveScene";
	// Save Scene As...
	nameCommand "icSaveSceneAs" -ann ($vendor + "Save Scene As") -c "source menus2Py; menus2Py.saveSceneAs()";
	hotkey -cmd -k "S" -n "icSaveSceneAs";
	hotkey -ctl -k "S" -n "icSaveSceneAs";
	// Incremental Save...
	nameCommand "icIncrementAndSave" -ann ($vendor + "Increment and Save") -c "source menus2Py; menus2Py.incrementAndSave()";
	hotkey -cmd -alt -k "s" -n "icIncrementAndSave";
	hotkey -ctl -alt -k "s" -n "icIncrementAndSave";

	// Instance the Icarus UI
	python("from core import icarus; session.icarus = icarus.app(app='maya')");

	// Initialise Scene Manager
	python("from tools.scenemanager import scenemanager; session.scnmgr = scenemanager.create(app='maya')");

	// Update Maya scene defaults at startup
	python("session.scnmgr.set_defaults()");
	//source gpsRenderSetup.mel; gpsRenderSetup.setCommonOptions();
}

// Load plugins
loadPlugin -quiet AbcExport;
loadPlugin -quiet AbcImport;
loadPlugin -quiet fbxmaya;
loadPlugin -quiet objExport;
loadPlugin -quiet OpenEXRLoader;
loadPlugin -quiet tiffFloatReader;
